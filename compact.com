$!
$!	COMPRESSION BIT SCHEME (3 BIT {8 NIBBLES} OF COLOR)
$!
$	DISP := WRITE SYS$OUTPUT
$	DISP "CCSAD%$@%$ COMPRESSION SCHEME "
$	IF P1.EQS."" THEN INQ/NOPUNC P1 " INPUT FILE ? "
$	IF P2.EQS."" THEN INQ/NOPUNC P2 "OUTPUT FILE ? "
$	OPEN/READ INPUT: 'P1'
$	OPEN/WRITE OUTPUT: 'P2'
$	OLINE := %
$	BYTELEFT = 6
$	BITLEFT = 8
$	ALLLINE = 0
$LINELP:
$	READ/END=FIN INPUT: ILINE
$	ALLLINE = ALLLINE+1
$	DISP "LINE ",ALLLINE
$	ILEN = 'F$LENGTH(ILINE)
$	IPOS = 1
$	RUN = 1
$	ILAST = %X'F$EXTRACT(0,1,ILINE)
$CHARLP:
$	INEW = %X'F$EXTRACT(IPOS,1,ILINE)
$	IF INEW.EQ.ILAST THEN $GOTO NXTCHR
$!
$!	HERE IF RUN LENGTH "RUN"
$!	 AND COLOR NIBBLE "ILAST"
$!
$	RETURNE := NEWCHR
$	GOTO ENCODE
$NEWCHR:
$!
$	RUN = 0
$	ILAST = 'INEW'
$NXTCHR:
$	RUN = RUN+1
$	IPOS = IPOS+1
$	IF IPOS.LT.ILEN THEN $GOTO CHARLP
$!	IGNORE LAST RUN OF COLOR ZERO
$	IF ILAST.EQ.0 THEN $GOTO EOL
$	RETURNE := EOL
$	GOTO ENCODE
$EOL:	RUN = 0		!MAXIMUM RUN LENGTH FOR END OF LINE (EOL)
$	ILAST = 1	!EOL LINE
$	RETURNE := LINELP
$	GOTO ENCODE
$!
$!	FINISHED ENCODING THE IMAGE
$!
$FIN:
$	RUN = 0		!MAXIMUM RUN LENGTH FOR END OF IMAGE (EOI)
$	ILAST = 0	!EOI LINE
$	RETURNE := EOI
$	GOTO ENCODE
$EOI:	IF BITLEFT.EQ.8 THEN $GOTO FIN2
$	BIT = 0
$	RETURN := EOI
$	GOTO WRBIT
$!
$FIN2:	IF BYTELEFT.EQ.6 THEN $GOTO FIN3
$	WRITE OUTPUT: "	FCB	",OLINE
$FIN3:	CLOSE INPUT:
$	CLOSE OUTPUT:
$	STOP
$!
$!	RUN LENGTH ENCODING ROUTINE, NEEDS "RUN", "ILAST", & "RETURNE"
$!
$ENCODE:
$	NRUN = 'RUN'
$	DISP "LINE ",ALLLINE,"	RUN ",RUN,"	COLOR ",ILAST
$	IF NRUN.LT.2 THEN $GOTO R1
$	IF NRUN.LT.6 THEN $GOTO R2
$	IF NRUN.LT.14 THEN $GOTO R4
$	IF NRUN.LT.30 THEN $GOTO R8
$	IF NRUN.LT.62 THEN $GOTO R16
$	IF NRUN.LT.126 THEN $GOTO R32
$	IF NRUN.LT.254 THEN $GOTO R64
$	DISP "OVERFLOW"
$	GOTO FIN
$R64:	BIT := 0
$	RETURN := R32
$	GOTO WRBIT
$R32:	BIT := 0
$	RETURN := R16
$	GOTO WRBIT
$R16:	BIT := 0
$	RETURN := R8
$	GOTO WRBIT
$R8:	BIT := 0
$	RETURN := R4
$	GOTO WRBIT
$R4:	BIT := 0
$	RETURN := R2
$	GOTO WRBIT
$R2:	BIT := 0
$	RETURN := R1
$	GOTO WRBIT
$R1:	NRUN = 'RUN'+2
$	IF NRUN.LT.4 THEN $GOTO D2
$	IF NRUN.LT.8 THEN $GOTO D3
$	IF NRUN.LT.16 THEN $GOTO D4
$	IF NRUN.LT.32 THEN $GOTO D5
$	IF NRUN.LT.64 THEN $GOTO D6
$	IF NRUN.LT.128 THEN $GOTO D7
$	IF NRUN.LT.255 THEN $GOTO D8
$D8:	BIT = 1
$	NRUN = NRUN-128
$	RETURN := D7
$	GOTO WRBIT
$D7:	BIT = 0
$	IF NRUN.LT.64 THEN $GOTO D7B
$	BIT = 1
$	NRUN = NRUN-64
$D7B:	RETURN := D6
$	GOTO WRBIT
$D6:	BIT = 0
$	IF NRUN.LT.32 THEN $GOTO D6B
$	BIT = 1
$	NRUN = NRUN-32
$D6B:	RETURN := D5
$	GOTO WRBIT
$D5:	BIT = 0
$	IF NRUN.LT.16 THEN $GOTO D5B
$	BIT = 1
$	NRUN = NRUN-16
$D5B:	RETURN := D4
$	GOTO WRBIT
$D4:	BIT = 0
$	IF NRUN.LT.8 THEN $GOTO D4B
$	BIT = 1
$	NRUN = NRUN-8
$D4B:	RETURN := D3
$	GOTO WRBIT
$D3:	BIT = 0
$	IF NRUN.LT.4 THEN $GOTO D3B
$	BIT = 1
$	NRUN = NRUN-4
$D3B:	RETURN := D2
$	GOTO WRBIT
$D2:	BIT = 0
$	IF NRUN.LT.2 THEN $GOTO D2B
$	BIT = 1
$	NRUN = NRUN-2
$D2B:	RETURN := D1
$	GOTO WRBIT
$D1:	BIT = 'NRUN'
$	RETURN := D0
$	GOTO WRBIT
$D0:
$!
$!	ENCODE COLOR NIBBLE
$!
$	NRUN = 'ILAST'
$	IF NRUN .GE. 8 THEN NRUN='NRUN'-7	
$!!!!!!!C4:	BIT = 0
$!!!!!!!	IF NRUN.LT.8 THEN $GOTO C4B
$!!!!!!!	BIT = 1
$!!!!!!!	NRUN = NRUN-8
$!!!!!!!C4B:	RETURN := C3
$!!!!!!!	GOTO WRBIT
$C3:	BIT = 0
$	IF NRUN.LT.4 THEN $GOTO C3B
$	BIT = 1
$	NRUN = NRUN-4
$C3B:	RETURN := C2
$	GOTO WRBIT
$C2:	BIT = 0
$	IF NRUN.LT.2 THEN $GOTO C2B
$	BIT = 1
$	NRUN = NRUN-2
$C2B:	RETURN := C1
$	GOTO WRBIT
$C1:	BIT = 'NRUN'
$	RETURN := C0
$	GOTO WRBIT
$C0:
$!	END OF RUN LENGTH ENCODE
$	GOTO 'RETURNE'
$!
$!	WRITE THE "BIT" SUBROUTINE, (NEEDS "RETURN")
$!
$WRBIT:
$	OLINE := 'OLINE''BIT'
$	BITLEFT = BITLEFT-1
$	IF BITLEFT.GT.0 THEN $GOTO 'RETURN'
$	BITLEFT = 8
$	BYTELEFT = BYTELEFT-1
$	IF BYTELEFT.GT.0 THEN $GOTO WRB2
$	BYTELEFT = 6
$	WRITE OUTPUT: "	FCB	",OLINE
$	OLINE := %
$	GOTO 'RETURN'
$WRB2:	OLINE := 'OLINE',%
$	GOTO 'RETURN'

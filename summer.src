	ORG	$B000
*
PRAM	EQU	0
*
*	SUM OF THE PART TABLE
*
SUMTBL	FDB	$0000,0		$F321 =
	FDB	$1000,0		$F323 = 
	FDB	$2000,0		$F325 = 
	FDB	$3000,0		$F327 = 
	FDB	$4000,0		$F329 = 
	FDB	$5000,0		$F32B = 
	FDB	$6000,0		$F32D = 
	FDB	$7000,0		$F32F = 
	FDB	$8000,0		$F331 = 
	FDB	$D000,0		$F33B = 
	FDB	$E000,0		$F33D = 
	FDB	$F000,0		$F33F = 
SUMEND	EQU	*	  FUDGE $F341 =
*
*	NORMAL F000 CHECKSUMS
*
START	ORCC	#$FF
	CMPS	#$BFFF
	BHI	START		BR=CREAMED PROGRAM, DON'T FORGET TO SET .S!
	CMPS	#$9000
	BLO	START		BR=CREAMED PROGRAM, DON'T FORGET TO SET .S!
	LDS	#$BF00
	LDU	#SUMTBL
10$	LDX	,U++
	LDY	#$1000		4K PARTS
	CLRA
20$	ADDA	,X+
	LEAY	-1,Y
	BNE	20$
	LDB	,U
	STD	,U++		REG.A = NEW CKSUM, REG.B = OLD CHECK SUM
	CMPU	#SUMEND
	BLO	10$
*
	TFR	A,B
	NEGB			MAKE F000 ADD UP TO 77
	ADDB	#$77	
	NOP			REG.A= F000 SUM, REG.B= NBR TO ADD UP TO $77
	LDU	#$A000		WORKSPACE
	PAGE
*
*	JZAP
*				REG.A ($6AD3) =
*				REG.B ($69D9) =
JZAP	LDX	JZAPST
	CLRA
1$	ADDA	,-X
	CMPX	#$D000
	BNE	1$
	TFR	A,B
	NEGB
	NOP			REG.A = IMMEDITATE DATA, REG.B=FUDGE DATA
2$
*
*	LZAP
*				REG.A ($85CD) =
*				REG.B ($8598) =
LZAP	LDX	LZAP1
	LDD	#34
1$	ADDA	,X+
	DECB
	BNE	1$
	TFR	A,B
	NEGB
	NOP			REG.A = IMMEDITATE DATA, REG.B=FUDGE DATA
2$
*
*	KZAP
*				REG.A ($EEC1) =
*				REG.B ($EB97) =
KZAP	CLR	PRAM,U
	CLR	PRAM+1,U
	CLR	PRAM+2,U
1$	LDX	PRAM,U
	LDA	,X++
	ADDA	PRAM+2,U
	STA	PRAM+2,U
	STX	PRAM,U
	BPL	1$
	TFR	A,B
	NEGB
10$	NOP			REG.A = IMMEDITATE DATA, REG.B=FUDGE DATA
	BRA	10$
*
KZAP1	FCB	0
JZAPST	FDB	$9000
LZAP1	FDB	$F320		F000 CHECKSUMS
*
	END	START
